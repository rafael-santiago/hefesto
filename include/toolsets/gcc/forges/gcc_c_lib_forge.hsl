#                                                Mon 08 Apr 2013 10:23:22 PM BRT
#
#                              Copyright (C) 2013 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#  "gcc_c_lib_forge.hsl"
#        by Rafael Santiago
#
# Description:
# ------------
# GCC compilation/build routines.
#
#       Relevant command line options to interact with this piece of code:
#
#       --forge-anyway: forces to build any forge involved in process.
#       --qsize=<number>: number of paralel compilation tasks.
#       --compile-model=<release|debug>: select for release or debug.
#                                        (the default is release)
#       --link-model=<static|shared>: select for shared or static.
#                                     (the default is static)
#       --ldflags=<linker flags>: flags that must be passed to linker.
#
# FINAL USERS, WARNING:
# --------------------
# Don't call any function here directly. Use the toolsets that use these
# functions in your projects declartions.
#


include ~/toolsets/gcc/forges/common.hsl
include ~/fsutil.hsl

var gcc_c_project_forge_dirty_files_nr type int;

function gcc_mk_lib_compilation_command(src_file type string, ofile type string,
                                        includes type string, cflags type string,
                                        is_release type int, is_static type int) : result type string {
    var cmd type string;
    if ($is_release == 1) {
        if ($is_static == 1) {
            $cmd = hefesto.toolset.compile_static_r($src_file, $ofile, $includes, $cflags);
        } else {
            $cmd = hefesto.toolset.compile_shared_r($src_file, $ofile, $includes, $cflags);
        }
    } else {
        if ($is_static == 1) {
            $cmd = hefesto.toolset.compile_static_d($src_file, $ofile, $includes, $cflags);
        } else {
            $cmd = hefesto.toolset.compile_shared_d($src_file, $ofile, $includes, $cflags);
        }
    }
    result $cmd;
}

function gcc_compile_source_list(sources type list, includes type string, cflags type string, obj_dir type string) : result type int {
    var size type int;
    var i type int;
    var exit_code type int;
    var ofile type string;
    var qsize_list type list;
    var run_list type list;
    var qsize type int;
    var compiled type list;
    var compile_model type list;
    var is_release type int;
    var curr_src_file type string;
    var link_model type list;
    var is_static type int;
    var regex type string;

    $size = $sources.count();
    if ($size == 0) result 0;
    $qsize_list = hefesto.sys.get_option("qsize");
    if ($qsize_list.count() > 0) {
        $qsize = $qsize_list.item(0);
    }
    $compile_model = hefesto.sys.get_option("compile-model");
    $is_release = 1;
    if ($compile_model.count() > 0) {
        if ($compile_model.item(0) != "release" &&
            $compile_model.item(0) != "debug") {
            hefesto.sys.echo(hefesto.project.toolset() + " internal error: unknown compile model: \"" + $compile_model.item(0) + "\"\n");
            result 1;
        }
        $is_release = ($compile_model.item(0) == "release");
    }
    $link_model = hefesto.sys.get_option("link-model");
    $is_static = 1;
    if ($link_model.count() > 0) {
        if ($link_model.item(0) != "static" &&
            $link_model.item(0) != "shared") {
            hefesto.sys.echo(hefesto.project.toolset() + " internal error: unknown link model: \"" + $compile_model.item(0) + "\"\n");
            result 1;
        }
        $is_static = ($link_model.item(0) == "shared");
    }
    $exit_code = 0;
    $i = 0;
    $gcc_c_project_forge_dirty_files_nr = 0;
    if ($qsize <= 1) {
        while ($i < $size && $exit_code == 0) {
            $ofile = $sources.item($i);
            $ofile = gcc_mk_ofile_name(filenamefrompath($ofile), $obj_dir);
            $regex = gcc_mk_regex_pattern_from_filepath($ofile);
            if (hefesto.toolset.file_has_change($sources.item($i)) == 1 || hefesto.sys.ls($regex) == 0) {
                $gcc_c_project_forge_dirty_files_nr = $gcc_c_project_forge_dirty_files_nr + 1;
                $curr_src_file = $sources.item($i);
                $exit_code = hefesto.sys.run(gcc_mk_lib_compilation_command($curr_src_file, $ofile, $includes, $cflags, $is_release, $is_static));
                if ($exit_code == 0) {
                    $compiled.add_item($sources.item($i));
                    hefesto.sys.echo("\t*** " + $sources.item($i) + ": no errors found.\n");
                } else {
                    hefesto.sys.echo("\t*** " + $sources.item($i) + ": errors found!\n");
                }
            } else {
                hefesto.sys.echo("\t*** " + $sources.item($i) + ": no changes.\n");
            }
            $i = $i + 1;
        }
        if ($gcc_c_project_forge_dirty_files_nr > 0) {
            hefesto.toolset.base_refresh($compiled);
        }
    } else {
        while ($i < $size) {
            $ofile = $sources.item($i);
            $ofile = gcc_mk_ofile_name(filenamefrompath($ofile), $obj_dir);
            $regex = gcc_mk_regex_pattern_from_filepath($ofile);
            if (hefesto.toolset.file_has_change($sources.item($i)) == 1 || hefesto.sys.ls($regex) == 0) {
                $gcc_c_project_forge_dirty_files_nr = $gcc_c_project_forge_dirty_files_nr + 1;
                $curr_src_file = $sources.item($i);
                $run_list.add_item(gcc_mk_lib_compilation_command($curr_src_file, $ofile, $includes, $cflags, $is_release, $is_static));
                $compiled.add_item($sources.item($i));
            }
            if ($run_list.count() == $qsize) {
                $exit_code = hefesto.sys.run($run_list);
                if ($exit_code == 0) {
                    hefesto.toolset.base_refresh($compiled);
                }
                $run_list.clear();
            }
            $i = $i + 1;
        }
        if($run_list.count() > 0) {
            $exit_code = hefesto.sys.run($run_list);
            if ($exit_code == 0) {
                hefesto.toolset.base_refresh($compiled);
            }
        }
    }
    result $exit_code;
}

function gcc_c_lib_forge(SOURCES type list, INCLUDES type list, CFLAGS type list, LIBNAME type string) : result type int {
    var exit_code type int;
    var includes type string;
    var cflags type string;
    var objects type string;
    var link_model type list;
    var is_static_library type string;

    if ($SOURCES.count() == 0) {
        hefesto.sys.echo("\t(empty source list)\n");
        result 0;
    }

    $link_model = hefesto.sys.get_option("link-model");
    $is_static_library = 1;
    if ($link_model.count() > 0) {
        if ($link_model.item(0) == "shared") {
            $is_static_library = 0;
        } else if ($link_model.item(0) != "static") {
            hefesto.sys.echo(hefesto.project.toolset() + " internal error: unknown link model: \"" + $link_model.item(0) + "\"\n");
            result 1;
        }
    }

    var obj_output_dir type list;
    var obj_dir type string;

    $obj_output_dir = hefesto.sys.get_option("obj-output-dir");
    if ($obj_output_dir.count() > 0) {
        $obj_dir = $obj_output_dir.item(0);
        mktree($obj_dir);
    } else {
        $obj_dir = hefesto.sys.pwd();
    }

    var bin_output_dir type list;
    var bin_output type string;

    $bin_output_dir = hefesto.sys.get_option("bin-output-dir");
    if ($bin_output_dir.count() > 0) {
        $bin_output = $bin_output_dir.item(0);
        mktree($bin_output);
        $bin_output = hefesto.sys.make_path($bin_output, $LIBNAME);
    } else {
        $bin_output = $LIBNAME;
    }

    $exit_code = 0;

    $includes = gcc_mk_gcc_incl_str_opt($INCLUDES);

    $cflags = gcc_mk_raw_str_opt($CFLAGS);

    var cpu_arch type list;
    $cpu_arch = hefesto.sys.get_option("cpu-arch");
    if ($cpu_arch.count() > 0) {
        var choosen_arch type string;
        $choosen_arch = $cpu_arch.item(0);
        if ($choosen_arch != "32" && $choosen_arch != "64") {
            hefesto.sys.echo(hefesto.project.toolset() + " fatal error: invalid value for cpu-arch option must be '32' or '64'.\n");
            result 1;
        }
        $cflags = $cflags + " -m" + $choosen_arch;
    }

    #  compiling...
    hefesto.sys.echo("*** compiling...\n");

    $exit_code = gcc_compile_source_list($SOURCES, $includes, $cflags, $obj_dir);

    var regex type string;
    $regex = gcc_mk_regex_pattern_from_filepath($bin_output);

    if ($gcc_c_project_forge_dirty_files_nr == 0 && hefesto.sys.ls($regex) == 0) {
        $gcc_c_project_forge_dirty_files_nr = 1;
    }

    if ($gcc_c_project_forge_dirty_files_nr > 0 && $exit_code == 0) {
        hefesto.sys.echo("*** now making .a file...\n\t*** waiting...\n");
        $objects = gcc_mk_ofiles_str_opt($SOURCES, $obj_dir);
        if ($is_static_library == 1) {
            $exit_code = gcc_ar_ofiles($bin_output, $objects);
        } else {
            $exit_code = gcc_so_ofiles($bin_output, $objects);
        }
    }

    if ($exit_code == 0) {
        hefesto.sys.echo("*** success.\n");
    } else {
        hefesto.sys.echo("*** failure.\n");
    }

    result $exit_code;
}

function gcc_ar_ofiles(libname type string, objects type string) : result type int {
    result hefesto.sys.run(hefesto.toolset.ar($libname, $objects));
}

function gcc_so_ofiles(libname type string, objects type string) : result type int {
    result hefesto.sys.run(hefesto.toolset.so($libname, $objects));
}
